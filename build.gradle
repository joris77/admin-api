buildscript {
	ext {
		springBootVersion = '1.4.4.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}") 
	}
}

apply plugin: 'java'
apply plugin: 'spring-boot'

configurations {
	cucumberRuntime {
		extendsFrom testRuntime
	}
}

task cucumberInt() {
	dependsOn compileTestJava
	doLast {
		javaexec {
			main = "cucumber.api.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
			if (project.hasProperty('jvmArgs')) {
				jvmArgs = [project.jvmArgs]
			}
			args = ['--plugin', 'pretty', '--glue', 'nl.smartworkx.admin.glue.integration', 'src/test/resources/features']
		}
	}
}

task javadocs(type: Javadoc) {
	source = sourceSets.main.allJava
}

task testjavadocs(type: Javadoc) {
	source = sourceSets.test.allJava
	destinationDir = reporting.file("test-docs")
}

test {
	filter {
		includeTestsMatching "*Test"
		exclude "*IntegrationTest"
	}
}

jar {
	baseName = 'admin-api'
	version = "1.0." + getTimestamp()
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.postgresql:postgresql:9.4-1206-jdbc42')
	compile('org.javamoney:moneta:1.0')
	compile('javax.money:money-api:1.0')
	compile('commons-lang:commons-lang:2.6')
    compile fileTree(dir: 'libs', include: '*.jar')
	compile('com.google.guava:guava:19.0')
	compile("org.springframework.boot:spring-boot-devtools")
	runtime('org.liquibase:liquibase-core')

	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile 'info.cukes:cucumber-java:1.2.4'
	testCompile 'info.cukes:cucumber-junit:1.2.4'
	testCompile 'info.cukes:cucumber-spring:1.2.4'
	testCompile 'com.jayway.jsonpath:json-path:2.2.0'
	testCompile group: 'junit', name: 'junit', version: '4.11'
}

springBoot {
	executable = true
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.12'
}

def getTimestamp() {
	def date = new Date()
	return date.format('yyMMdd.HHmmss')
}

apply plugin: 'java'
