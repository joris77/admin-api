buildscript {
	ext {
		springBootVersion = '1.3.3.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}") 
	}
}

apply plugin: 'java'
apply plugin: 'spring-boot'

configurations {
	cucumberRuntime {
		extendsFrom testRuntime
	}
}

task cucumberInt() {
	dependsOn assemble, compileTestJava
	doLast {
		javaexec {
			main = "cucumber.api.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
			args = ['--plugin', 'pretty', '--glue', 'nl.smartworkx.admin.glue.integration', 'src/test/resources/features']
		}
	}
}

task javadocs(type: Javadoc) {
	source = sourceSets.main.allJava
}

task testjavadocs(type: Javadoc) {
	source = sourceSets.test.allJava
	destinationDir = reporting.file("test-docs")
}

jar {
	baseName = 'admin-api'
	version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.postgresql:postgresql:9.4-1206-jdbc42')
	compile('org.javamoney:moneta:1.0')
	compile('javax.money:money-api:1.0')
	compile('commons-lang:commons-lang:2.6')
    compile fileTree(dir: 'libs', include: '*.jar')
	compile('com.google.guava:guava:19.0')
	runtime('org.flywaydb:flyway-core')

	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile 'info.cukes:cucumber-java:1.2.4'
	testCompile 'info.cukes:cucumber-junit:1.2.4'
	testCompile 'info.cukes:cucumber-spring:1.2.4'
	testCompile 'com.jayway.jsonpath:json-path:2.2.0'
}

springBoot {
	executable = true
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.12'
}
